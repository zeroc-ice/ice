// **********************************************************************
//
// Copyright (c) 2003-2015 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

['icebox', 'simple'].each {
    def name = it
    project(":demoIceGrid${it.capitalize()}") {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            compile localDependency("ice")
            compile localDependency("icebox")
            compile localDependency("icegrid")
            compile localDependency("icelocatordiscovery")
            if (!useLocalOnly) {
                runtime "org.apache.tools:bzip2:1.0"
            }
        }

        task makeDbDirs << {
            file('db/registry').mkdirs()
            file('db/node').mkdirs()
        }
        compileJava.dependsOn(makeDbDirs)

        clean {
            delete 'db'
        }

        // Don't create the default jar.
        jar.enabled = false

        demoJar("client")
        demoJar("server")
    }
}
