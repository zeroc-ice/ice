// **********************************************************************
//
// Copyright (c) 2003-2016 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

subprojects {
    project.version = iceVersion
    project.group = "com.zeroc"

    apply plugin: 'java'
    apply plugin: 'idea'
    apply from: "$rootProject.projectDir/gradle/ice.gradle"


    repositories {
        if (slice.jarDir) {
            flatDir {
                name "local"
                dirs slice.jarDir
            }
        }
        
        if(new File("/usr/share/maven-repo").exists()) {
            maven {
                url "/usr/share/maven-repo"
            }
        }
        
        maven {
            url 'https://repo.zeroc.com/nexus/content/repositories/thirdparty'
        }
    }

    jar {
        manifest {
            attributes("Built-By": "ZeroC, Inc.")
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xlint:-options"
    }

    idea.module {
        excludeDirs -= file(buildDir)
        buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
    }
}

task dist()
dist.dependsOn(project(":ice").assemble)
dist.dependsOn(project(":freeze").assemble) 
dist.dependsOn(project(":glacier2").assemble)
dist.dependsOn(project(":icegrid").assemble)
dist.dependsOn(project(":icebox").assemble) 
dist.dependsOn(project(":icediscovery").assemble) 
dist.dependsOn(project(":icepatch2").assemble)
dist.dependsOn(project(":icestorm").assemble)
dist.dependsOn(project(":IceGridGUI").assemble)

//
// Used to build the gradle wrapper to automatically download and install
// the version of gradle needed to build Ice.
//
task wrapper(type: Wrapper) {
    gradleVersion = "2.6"
}
