// Copyright (c) ZeroC, Inc.

buildscript {
    repositories {
        mavenCentral()
    }
}

def runningInCi = providers.environmentVariable("CI").isPresent()

apply plugin: 'checkstyle'
apply plugin: 'org.openrewrite.rewrite'

slice {
    includeSearchPath = files("${project.ext.topSrcDir}/slice")
    def defaultIceToolsPath = "${new File(project.ext.topSrcDir).getCanonicalPath()}/cpp/bin"

    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        def platform = project.findProperty("cppPlatform") ?: "x64"
        def configuration = project.findProperty("cppConfiguration") ?: "Release"
        toolsPath = "$defaultIceToolsPath/$platform/$configuration"
    } else {
        toolsPath = defaultIceToolsPath
    }
}

checkstyle {
    toolVersion = '10.21.4'

    // If we're running in CI, we want the build to fail if any warnings are emitted.
    // This doesn't affect what checkstyle emits, just whether it 'fails' or 'succeeds' from Gradle's perspective.
    if (runningInCi) {
        maxWarnings = 0
    }
}

tasks.withType(Checkstyle).configureEach {
    // Ensure that running checkstyle multiple times always yields the same output. Otherwise, after an initial run,
    // the checkstyle task will be marked "UP TO DATE", and subsequent runs will skip over it.
    outputs.upToDateWhen { false }
    doNotTrackState("Always run Checkstyle")
}
ext.libDir = "$rootProject.projectDir/lib"
