// Copyright (c) ZeroC, Inc.

buildscript {
    repositories {
        mavenCentral()

        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"
    }
}

apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin

slice {
    includeSearchPath = files("${project.ext.topSrcDir}/slice")
    def defaultIceToolsPath = "${new File(project.ext.topSrcDir).getCanonicalPath()}/cpp/bin"

    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        def platform = project.findProperty("cppPlatform") ?: "x64"
        def configuration = project.findProperty("cppConfiguration") ?: "Release"
        toolsPath = "$defaultIceToolsPath/$platform/$configuration"
    } else {
        toolsPath = defaultIceToolsPath
    }
}

spotless {
  format 'misc', {
    // define the files to apply `misc` to
    target '*.java', '*.gradle'

    // define the steps to apply to those files
    trimTrailingWhitespace()
    endWithNewline()
  }

  java {
    toggleOffOn()

    target project.fileTree("src/main/java") {
        include '**/*.java'
        exclude 'com/zeroc/Ice/PropertyNames.java'
    }

    // Remove any unused imports, then ensure the remaining imports are correctly ordered.
    removeUnusedImports()

    // Linting
    cleanthat()

    // We use google-java-format to format our Java code. We also set reorderImports and formatJavadoc to true to
    // match the default behavior of the google-java-format tool.
    // When updating the version be sure to update the version in `.vscode/settings.json` as well.
    googleJavaFormat("1.24.0").aosp().reorderImports(true).formatJavadoc(true)
    formatAnnotations()
  }
}

// Android does not have a compileJava task
if(!(project.hasProperty('android') && project.android.sourceSets)) {
    compileJava {
        // Set the target release using the --release option
        options.compilerArgs.addAll(['--release', "${targetJavaRelease}"])
        options.debug = debug
    }
}

// If the prefix isn't set use these default locations.
if(!prefix) {
    def prefixVersion = iceVersion
    if(prefixVersion.indexOf(".0-alpha")){
        prefixVersion = prefixVersion.replace(".0-alpha", "a")
    } else if(prefixVersion.indexOf(".0-beta")){
        prefixVersion = prefixVersion.replace(".0-beta", "b")
    }
    if(System.properties['os.name'].toLowerCase().contains('windows')) {
        prefix = "C:\\Ice-${prefixVersion}"
    } else {
        prefix = "/opt/Ice-${prefixVersion}"
    }
}

// Installation location for jar/pom & executables.
if((prefix.startsWith("/usr") || prefix.startsWith("/usr/local"))) {
    ext.jarDir = prefix + "/share/java"
    ext.binDir = prefix + "/bin"
} else {
    ext.jarDir = prefix + "/lib"
    ext.binDir = prefix + "/bin"
}

ext.libDir = "$rootProject.projectDir/lib"
