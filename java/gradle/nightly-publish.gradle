// Copyright (c) ZeroC, Inc.

ext.mavenRepository = System.getenv('MAVEN_REPOSITORY')
ext.mavenUsername = System.getenv('MAVEN_USERNAME')
ext.mavenPassword = System.getenv('MAVEN_PASSWORD')

publishing {
    publications {
        create("sliceTools", MavenPublication) {
            groupId = "com.zeroc"
            artifactId = "slice-tools"
            version = iceVersion

            artifact file("tools/slice-tools/build/libs/slice-tools-${iceVersion}.jar")
        }

        file("lib").eachFileMatch(~/^(.*)-${iceVersion}\.jar$/) { File jarFile ->
            def baseName = jarFile.name.replaceAll(/-${iceVersion}\.jar$/, "")
            def artifactId = baseName
            def pubName = artifactId.replaceAll("[^a-zA-Z0-9]", "")

            create(pubName, MavenPublication) {
                groupId = "com.zeroc"
                artifactId = artifactId
                version = iceVersion

                artifact jarFile
                artifact(file("${jarFile}.asc")) {
                    extension = "jar.asc"
                    classifier = null
                }

                artifact file("lib/${artifactId}-${iceVersion}-sources.jar") {
                    classifier = "sources"
                }
                artifact(file("lib/${artifactId}-${iceVersion}-sources.jar.asc")) {
                    extension = "jar.asc"
                    classifier = null
                }

                artifact file("lib/${artifactId}-${iceVersion}-javadoc.jar") {
                    classifier = "javadoc"
                }
                artifact file("lib/${artifactId}-${iceVersion}-javadoc.jar.asc") {
                    extension = "jar.asc"
                    classifier = null
                }

                pom.withXml {
                    def pomFile = file("lib/${artifactId}-${iceVersion}.pom")
                    if (pomFile.exists()) {
                        asNode().appendNode("rawPom", pomFile.text)
                    }
                }
                artifact(file("lib/${artifactId}-${iceVersion}.pom.asc")) {
                    extension = "pom.asc"
                    classifier = null
                }
            }
        }
    }

    repositories {
        maven {
            url = mavenRepository
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
        }
    }
}
