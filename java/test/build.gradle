// Copyright (c) ZeroC, Inc.

ext.testDir = "${projectDir}/src/main/java/test"

// Don't generate javadoc
javadoc.enabled = false

sourceSets {
    main {
        java {
            exclude 'plugins'
        }

        slice {
            srcDir("${testDir}")
        }
    }
}

dependencies {
    implementation project(':ice')
    runtimeOnly project(':icediscovery')
    implementation project(':icelocatordiscovery')
    implementation project(':icebox')
    implementation project(':glacier2')
    implementation project(':icestorm')
    implementation project(':icegrid')
    implementation project(':testPlugins')
}

if(!gradle.startParameter.isOffline()) {
    dependencies {
        runtimeOnly "org.apache.commons:commons-compress:1.20"
    }
}

jar {
    archiveFileName = "test.jar"
    destinationDirectory = new File("$rootProject.projectDir/lib/")
    manifest {
        attributes("Class-Path": configurations.runtimeClasspath.resolve().collect { it.toURI() }.join(' '))
    }
}

clean {
    delete("$rootProject.projectDir/lib/test.jar")
    ['src/main/java/test/IceGrid/simple/db'].each {
        delete fileTree(dir: it)
    }
    delete("src/main/java/test/Slice/generation/classes")
}
