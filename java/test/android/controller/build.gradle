// Copyright (c) ZeroC, Inc.

buildscript {
    repositories {
        google()
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.11.0'
    }
}

plugins {
    id 'com.zeroc.ice.slice-tools' apply false
}

apply plugin: 'com.android.application'
apply plugin: 'com.zeroc.ice.slice-tools'

def topSrcDir = new File("$rootProject.projectDir/../../../..").getCanonicalPath()

slice {
    def defaultIceToolsPath = "${topSrcDir}/cpp/bin"

    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        def platform = project.findProperty("cppPlatform") ?: "x64"
        def configuration = project.findProperty("cppConfiguration") ?: "Release"
        toolsPath = "$defaultIceToolsPath/$platform/$configuration"
    } else {
        toolsPath = defaultIceToolsPath
    }
}

repositories {
    google()
    mavenCentral()
}

android {
    compileSdk = 34

    defaultConfig {
        applicationId "com.zeroc.testcontroller"
        minSdkVersion = 34 // SDK 34 required for Java 17 compatibility
        targetSdkVersion = 34
        multiDexEnabled = true // Necessary otherwise we'd exceed the 64K DEX limit.
        compileOptions {
            // Sets Java compatibility to Java 17
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            // Set minifyEnabled to false because the test app loads classes dynamically.
            minifyEnabled = false
        }

        release {
            // Set minifyEnabled to false because the test app loads classes dynamically.
            minifyEnabled = false
        }
    }
    namespace = 'com.zeroc.testcontroller'

    sourceSets {
        main {
            slice {
                srcDir "$topSrcDir/scripts"
            }
        }
    }
}

task copyCertificatesTask(type: Copy) {
    from "${rootProject.projectDir}/../../../../certs/common/ca/ca.p12"
    from "${rootProject.projectDir}/../../../../certs/common/ca/client.p12"
    from "${rootProject.projectDir}/../../../../certs/common/ca/server.p12"
    into "src/main/res/raw"
}
preBuild.dependsOn(copyCertificatesTask)

clean {
    delete("src/main/res/raw/ca.p12")
    delete("src/main/res/raw/client.p12")
    delete("src/main/res/raw/server.p12")
}

dependencies {
    implementation files("${rootProject.projectDir}/../../../lib/glacier2-${project.iceVersion}.jar")
    implementation files("${rootProject.projectDir}/../../../lib/ice-${project.iceVersion}.jar")
    runtimeOnly files("${rootProject.projectDir}/../../../lib/icediscovery-${project.iceVersion}.jar")
    runtimeOnly files("${rootProject.projectDir}/../../../lib/icebt-${project.iceVersion}.jar")

    implementation files("${rootProject.projectDir}/../../../lib/test.jar")
    runtimeOnly "org.apache.commons:commons-compress:1.20"
}
