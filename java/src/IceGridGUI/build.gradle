// Copyright (c) ZeroC, Inc.

project.ext.displayName = "IceGridGUI"
project.ext.description = ""

ext.os = System.properties['os.name']
if(os == "Mac OS X") {
    ext.platform = "mac"
} else if(os.toLowerCase().contains("windows")) {
    ext.platform = "win"
} else {
    ext.platform = "linux"
}


// Don't generate javadoc
javadoc.enabled = false

// Disable deprecation warnings caused by JGoodies
tasks.withType(JavaCompile) {
    options.deprecation = false
}

dependencies {
      implementation project(':ice')
      implementation project(':icelocatordiscovery')
      implementation project(':icebox')
      implementation project(':icestorm')
      implementation project(':glacier2')
      implementation project(':icegrid')
      implementation "com.jgoodies:jgoodies-looks:${jgoodiesLooksVersion}"
      implementation "com.jgoodies:jgoodies-forms:${jgoodiesFormsVersion}"

    implementation "org.openjfx:javafx-base:${openjfxVersion}:${platform}"
    implementation "org.openjfx:javafx-swing:${openjfxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${openjfxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${openjfxVersion}:${platform}"
}

project.ext.tmpJarName = "IceGridGUITEMP.jar"
project.ext.jarName = "icegridgui.jar"

jar {
    archiveFileName = tmpJarName
    manifest {
        attributes("Main-Class": "com.zeroc.IceGridGUI.Main",
                   "Built-By": "ZeroC, Inc.")
    }
}

project.ext.libJars = []

apply from: "proguard-jar.gradle"

clean {
    delete("${libDir}/${jarName}")
    delete("${libDir}/IceGrid GUI.app")
}
