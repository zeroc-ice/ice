// Copyright (c) ZeroC, Inc.

project.ext.displayName = "IceGridGUI"
project.ext.description = ""

ext.hasJavaFx = false

if(iceGridGuiUseJavaFX.toBoolean()) {
    ext.os = System.properties['os.name']
    if(os == "Mac OS X") {
        ext.platform = "mac"
        ext.hasJavaFx = true
    } else if(os.toLowerCase().contains("windows")) {
        ext.platform = "win"
        ext.hasJavaFx = true
    } else if(os.toLowerCase().contains("linux")) {
        ext.platform = "linux"
        ext.hasJavaFx = true
    } else {
        ext.hasJavaFx = false
    }
}

if(!hasJavaFx) {
    sourceSets {
        main {
            java {
                exclude '**/LiveDeployment/GraphView.java'
            }
        }
    }
}

// Don't generate javadoc
javadoc.enabled = false

// Disable deprecation warnings caused by JGoodies
tasks.withType(JavaCompile) {
    options.deprecation = false
}

dependencies {
      implementation project(':ice')
      implementation project(':icelocatordiscovery')
      implementation project(':icebox')
      implementation project(':icestorm')
      implementation project(':glacier2')
      implementation project(':icegrid')
      implementation "com.jgoodies:jgoodies-looks:${jgoodiesLooksVersion}"
      implementation "com.jgoodies:jgoodies-forms:${jgoodiesFormsVersion}"

      if(hasJavaFx) {
           implementation "org.openjfx:javafx-base:${openjfxVersion}:${platform}"
           implementation "org.openjfx:javafx-swing:${openjfxVersion}:${platform}"
           implementation "org.openjfx:javafx-controls:${openjfxVersion}:${platform}"
           implementation "org.openjfx:javafx-graphics:${openjfxVersion}:${platform}"
      }
}

project.ext.tmpJarName = "IceGridGUITEMP.jar"
project.ext.jarName = "icegridgui.jar"

jar {
    archiveFileName = tmpJarName
    manifest {
        attributes("Main-Class": "com.zeroc.IceGridGUI.Main",
                   "Built-By": "ZeroC, Inc.")
    }
}

project.ext.libJars = []

project.ext.jarBuilder = icegridguiProguard.toBoolean() ? "proguard-jar.gradle" : "plain-jar.gradle"

apply from: jarBuilder

clean {
    delete("${libDir}/${jarName}")
    delete("${libDir}/IceGrid GUI.app")
}
