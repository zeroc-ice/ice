// **********************************************************************
//
// Copyright (c) 2003-2018 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

project.ext.displayName = "IceGridGUI"
project.ext.description = ""

if(GradleVersion.current() >= GradleVersion.version('4.1') && JavaVersion.current() > JavaVersion.VERSION_1_10) {
    ext.os = System.properties['os.name']
    if(os == "Mac OS X") {
        ext.platform = "mac"
        ext.hasJavaFx = true
    } else if(os.toLowerCase().contains("windows")) {
        ext.platform = "win"
        ext.hasJavaFx = true
    } else if(os.toLowerCase().contains("linux")) {
        ext.platform = "linux"
        ext.hasJavaFx = true
    } else {
        ext.hasJavaFx = false
    }
} else {
    ext.javafxJar = ['jfxrt.jar',
                     'lib/jfxrt.jar',
                     'lib/ext/jfxrt.jar',
                     'jre/lib/jfxrt.jar',
                     'jre/lib/ext/jfxrt.jar',
                     'lib/javafx-swt.jar'].find{ new File("${System.properties['java.home']}/${it}").exists() }

    ext.hasJavaFx = javafxJar != null
}

if(!hasJavaFx) {
    sourceSets {
        main {
            java {
                exclude '**/LiveDeployment/GraphView.java'
            }
        }
    }
}

// Don't generate javadoc
javadoc.enabled = false

// Disable deprecation warnings caused by JGoodies
tasks.withType(JavaCompile) {
    options.deprecation = false
}

dependencies {
      implementation project(':ice')
      implementation project(':icessl')
      implementation project(':icelocatordiscovery')
      implementation project(':icebox')
      implementation project(':icestorm')
      implementation project(':glacier2')
      implementation project(':icegrid')
      implementation "com.jgoodies:jgoodies-looks:${jgoodiesLooksVersion}"
      implementation "com.jgoodies:jgoodies-forms:${jgoodiesFormsVersion}"

      if(hasJavaFx && GradleVersion.current() >= GradleVersion.version('4.1') && JavaVersion.current() > JavaVersion.VERSION_1_10) {
           implementation "org.openjfx:javafx-base:11:$platform"
           implementation "org.openjfx:javafx-swing:11:$platform"
           implementation "org.openjfx:javafx-controls:11:$platform"
           implementation "org.openjfx:javafx-graphics:11:$platform"
      }
}

project.ext.tmpJarName = "IceGridGUITEMP.jar"
project.ext.jarName = "icegridgui.jar"

jar {
    archiveName = tmpJarName
    manifest {
        attributes("Main-Class": "com.zeroc.IceGridGUI.Main",
                   "Built-By": "ZeroC, Inc.")
    }
}

project.ext.libJars = []

project.ext.jarBuilder = "plain-jar.gradle"
if(icegridguiProguard.toBoolean()) {
    if(GradleVersion.current() >= GradleVersion.version('4.1') && JavaVersion.current() > JavaVersion.VERSION_1_10) {
        logger.warn('Proguard does not yet support Java 11. Plain IceGridGUI JAR will be built.')
    }
    else {
        project.ext.jarBuilder = "proguard-jar.gradle"
    }
}

apply from: jarBuilder

clean {
    delete("${libDir}/${jarName}")
    delete("${libDir}/IceGrid GUI.app")
}
