// **********************************************************************
//
// Copyright (c) 2003-2018 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "net.sf.proguard:proguard-gradle:6.0.2"
    }
}

configurations {
    bundleapp
}

dependencies {
      bundleapp 'com.panayotis:appbundler:1.1.0'
}

if(JavaVersion.current() == JavaVersion.VERSION_1_8) {
    ['rt.jar',
     'jsse.jar',
     'i386/default/jclSC170/vm.jar',
     'amd64/default/jclSC170/vm.jar',
     'ibmcertpathfw.jar',
     'math.jar'].each {
        def jfile = searchFile(["${System.properties['java.home']}"], ["${it}", "lib/${it}", "jre/lib/${it}"])
        if(jfile) {
            project.ext.libJars << jfile
        }
    }

    if(hasJavaFx) {
        project.ext.libJars << "${System.properties['java.home']}/${javafxJar}"
    }
}
else {
    ['java.base.jmod',
     'java.xml.jmod',
     'java.desktop.jmod',
     'java.prefs.jmod',
     'java.naming.jmod',
     'java.datatransfer.jmod',
     'javafx.base.jmod',
     'javafx.controls.jmod',
     'javafx.graphics.jmod',
     'javafx.swing.jmod',
     'java.logging.jmod'].each {
        project.ext.libJars << "${System.properties['java.home']}/jmods/${it}"
    }
}

task proguardJar(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    injars configurations.runtimeClasspath.resolve(), filter: "!META-INF/**"
    injars "${projectDir}/build/libs/${tmpJarName}"
    outjars "${libDir}/${jarName}"
    libraryjars project.ext.libJars
    configuration 'icegridgui.pro'
}

task copyJars(type: Copy, dependsOn: proguardJar) {
    from new File("${libDir}/${jarName}")
    into "${DESTDIR}${jarDir}"
}
task install(dependsOn: copyJars)

if(System.properties['os.name'] == "Mac OS X") {
    def appName = "IceGrid GUI"
    task bundleapp(dependsOn: proguardJar) {
        doLast {
            ant.taskdef(name: 'bundleapp',
                        classname: 'com.oracle.appbundler.AppBundlerTask',
                        classpath: configurations.bundleapp.asPath)

            ant.bundleapp(outputdirectory: "${libDir}",
                          name: appName,
                          displayname: appName,
                          identifier: "com.zeroc.IceGridGUI",
                          icon: "${projectDir}/src/main/resources/icons/icegrid.icns",
                          shortversion: "${project.version}",
                          applicationCategory: "public.app-category.utilities",
                          mainclassname: "com.zeroc.IceGridGUI/Main",
                          copyright: "Copyright Â© 2005-2018 ZeroC, Inc. All rights reserved.") {
                classpath(file: "${libDir}/${jarName}") {}
                option(value: "-Dapple.laf.useScreenMenuBar=true") {}
                option(value: "-Dcom.apple.macos.use-file-dialog-packages=true") {}
                option(value: "-Dcom.apple.macos.useScreenMenuBar=true") {}
                option(value: "-Xdock:name=IceGrid GUI") {}
                option(value: "-Dcom.apple.mrj.application.apple.menu.about.name=${appName}") {}
            }
        }
    }
    assemble.dependsOn(bundleapp)

    task copyBundle(type: Copy, dependsOn: bundleapp) {
        from "${libDir}/${appName}.app"
        into "${DESTDIR}/${prefix}/${appName}.app"
    }

    install.dependsOn(copyBundle)
}
else {
    assemble.dependsOn(proguardJar)
}
