 //**********************************************************************
//
// Copyright (c) 2003-2017 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

apply plugin: 'java'
apply from: "$rootProject.projectDir/../java/gradle/ice.gradle"
ext.testDir = "$rootProject.projectDir/../java/test/src/main/java/test"
apply from: "slice.gradle"
apply from: "$rootProject.projectDir/gradle/tests.gradle"

sourceCompatibility = iceSourceCompatibility
targetCompatibility = iceTargetCompatibility

sourceSets {
    main {
        java {
            srcDir "$rootProject.projectDir/../java/test/src/main/java"
            include 'test/Util/**'
        }
    }
}

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
}

dependencies {
    compile localDependency('ice')
}

jar {
    archiveName = "test.jar"
}

tests.each {
    def dirName = it
    def name = it.replace('/', '_')

    project(":test_${name}") {
        apply plugin: 'java'
        apply from: "$rootProject.projectDir/../java/gradle/ice.gradle"

        sourceCompatibility = iceSourceCompatibility
        targetCompatibility = iceTargetCompatibility

        sourceSets {
            main {
                java {
                    srcDir "$rootProject.projectDir/../java/test/src/main/java"
                    include "test/${dirName}/**"
                    srcDir "$rootProject.projectDir/test/build/generated-src/"
                    include "test/${dirName}/**"
                }
            }
        }

        dependencies {
            compile project(':test')
            compile localDependency('ice')
            compile localDependency('icessl')
        }
    }
}
