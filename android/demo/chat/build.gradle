// **********************************************************************
//
// Copyright (c) 2003-2014 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

apply plugin: 'com.android.application'
apply from: "$rootProject.projectDir/../java/gradle/ice.gradle"
apply plugin: 'slice'

slice {
    java {
        set1 {
            include = ["${projectDir}/src/main/slice", "${sliceDir}"]
            srcDir = 'src/main'
        }
    }
}

android {
    compileSdkVersion ice_compileSdkVersion
    buildToolsVersion ice_buildToolsVersion

    defaultConfig {
        applicationId "com.zeroc.chat"
        minSdkVersion ice_minSdkVersion
        targetSdkVersion ice_targetSdkVersion
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_6
            targetCompatibility JavaVersion.VERSION_1_6
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    // It would be ideal if the slice plugin did this, but it doesn't work as expected.
    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', project.slice.output ]
            }
        }
    }
}

task copyBksTask(type: Copy) {
    from "${rootProject.projectDir}/../certs/client.bks"
    into "src/main/res/raw"
}
preBuild.dependsOn(copyBksTask)

clean {
    delete("src/main/res/raw/client.bks")
}

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
}

dependencies {
    compile localDependency('ice')
    compile localDependency('glacier2')
}
