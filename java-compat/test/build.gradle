// **********************************************************************
//
// Copyright (c) 2003-2017 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext.testDir = "${projectDir}/src/main/java/test"
apply from: "slice.gradle"

def hasLambda = org.gradle.internal.jvm.Jvm.current().getJavaVersion().toString().toFloat() >= 1.8f

sourceSets {
    main {
        java {
            if(!hasLambda) {
                exclude '**/lambda'
            }
            exclude 'plugins'
            exclude 'ejb'
        }
    }
}

dependencies {
    compile localDependency('ice-compat')
    compile localDependency('icediscovery-compat')
    compile localDependency('icelocatordiscovery-compat')
    compile localDependency('icebox-compat')
    compile localDependency('glacier2-compat')
    compile localDependency('icestorm-compat')
    compile localDependency('icegrid-compat')
    compile project(':testPlugins')
}

if(!gradle.startParameter.isOffline()) {
    dependencies {
        runtime "org.apache.commons:commons-compress:1.14"
    }
}

jar {
    archiveName = "test.jar"
    destinationDir = new File("$rootProject.projectDir/lib/")
    manifest {
        attributes("Class-Path": configurations.runtime.resolve().collect { it.toURI() }.join(' '))
    }
}

clean {
    delete("$rootProject.projectDir/lib/test.jar")
    ['src/main/java/test/IceGrid/simple/db'].each {
        delete fileTree(dir: it)
    }
    delete("src/main/java/test/Slice/generation/classes")
}
