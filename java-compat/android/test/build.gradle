 //**********************************************************************
//
// Copyright (c) 2003-2018 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

apply plugin: 'java'
apply from: "$project.ext.topSrcDir/java/gradle/ice.gradle"
slice.compat = true
ext.testDir = "$project.ext.topSrcDir/java-compat/test/src/main/java/test"
apply from: "$project.ext.topSrcDir/java-compat/test/slice.gradle"
apply from: "$rootProject.projectDir/gradle/tests.gradle"
apply from: "$rootProject.projectDir/gradle/dex.gradle"

sourceCompatibility = 1.7
targetCompatibility = 1.7

sourceSets {
    main {
        java {
            srcDir "$project.ext.topSrcDir/java-compat/test/src/main/java"
            include 'test/Util/**'
            exclude 'test/Ice/**/lambda'
        }
    }
}

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
}

dependencies {
    compile localDependency('ice-compat')
}

tests.each {
    def dirName = it
    def name = it.replace('/', '_')
    project(":test_${name}") {
        apply plugin: 'java'
        apply from: "$project.ext.topSrcDir/java/gradle/ice.gradle"
        slice.compat = true

        sourceCompatibility = 1.7
        targetCompatibility = 1.7

        sourceSets {
            main {
                java {
                    srcDir "$project.ext.topSrcDir/java-compat/test/src/main/java"
                    include "test/${dirName}/**"
                    exclude "test/${dirName}/lambda"
                    srcDir "$rootProject.projectDir/test/build/generated-src/"
                    include "test/${dirName}/**"
                }
            }
        }

        def dexName = "test_${name}.dex"
        task dexClasses {
            inputs.dir "${buildDir}/classes/java/main"
            outputs.file "${buildDir}/classes/java/main/${dexName}"
            doLast {
                exec {
                    commandLine "${dxCmd}", '--dex',
                            "--output=${buildDir}/classes/java/main/${dexName}",
                            "${buildDir}/classes/java/main"
                }
            }
        }

        dexClasses.dependsOn('classes')

        configure(jar) {
            include "${dexName}"
        }
        jar.dependsOn(dexClasses)

        dependencies {
            compile project(':test')
            compile localDependency('ice-compat')
        }
    }
}
